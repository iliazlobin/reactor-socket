buildscript {
  repositories {
    mavenCentral()
    maven { url = "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("gradle.plugin.com.dorongold.plugins:task-tree:1.3")
    classpath("io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${project.properties['kotlin.version']}")
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
  }
}

apply {
  plugin("com.dorongold.task-tree")
  plugin("io.spring.dependency-management")
  plugin("kotlin-platform-jvm")
  plugin("org.springframework.boot")
  plugin("maven-publish")
}

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
  mavenCentral()
  maven { url = "https://repo.spring.io/milestone" }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-dependencies:2.0.0.M7"
    mavenBom "io.projectreactor:reactor-bom:Bismuth-RELEASE"
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url = "https://repo.spring.io/milestone" }
}

dependencies {
  // logging
  compile("ch.qos.logback:logback-classic:1.2.3")

  // sx
  compile("sx.react.ext:reactor-ext:1.0-SNAPSHOT") { changing = true }

  // kotlin
  compile("org.jetbrains.kotlin:kotlin-stdlib-jre8")
  compile("org.jetbrains.kotlin:kotlin-reflect")

  // spring
  compile("org.springframework.boot:spring-boot-starter")
  compile("org.springframework:spring-core")

  // utils
  compile("io.vavr:vavr-kotlin:0.9.2") {
    exclude module: "kotlin-stdlib-jre8"
  }
  compile("io.projectreactor:reactor-core")
  compile("io.netty:netty-buffer:4.1.19.Final")

  // test
  testCompile("org.junit.jupiter:junit-jupiter-api")
  testRuntime("org.junit.jupiter:junit-jupiter-engine")
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile("org.springframework:spring-test")
}

task sourceJar(type: Jar) {
  from sourceSets.main.java.srcDirs
  from sourceSets.main.kotlin.srcDirs
  classifier "sources"
  extension "jar"
}

task javadocJar(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId "${project.group}"
      artifactId "${project.name}"
      version "${project.version}"
      from components.java
      artifact sourceJar
      artifact javadocJar
    }
  }
}

configurations.all {
  resolutionStrategy {
    cacheDynamicVersionsFor 0, 'seconds'
    cacheChangingModulesFor 0, 'seconds'
  }
}

sourceSets {
  main.kotlin.srcDirs += main.java.srcDirs
}

compileJava {
  options.compilerArgs += ["-Xlint:unchecked"]
}
compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

task art() {
  println("${project.group}:${project.name}:${project.version}")
}

taskTree {
  noRepeat = true
  impliesSubProjects = true
}
